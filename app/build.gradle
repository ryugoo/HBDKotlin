apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Version
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

// Package
def packageName = "com.r384ta.android.hbdkotlin" // input here

// Library version
ext {
  support_lib_version = '23.1.1'
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId packageName
    minSdkVersion 21
    targetSdkVersion 23
    versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }
  buildTypes {
    release {
      shrinkResources true
      debuggable false
      jniDebuggable false
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      applicationIdSuffix '.debug'
      versionNameSuffix '-dev'
      shrinkResources true
      debuggable true
      jniDebuggable true
      minifyEnabled false
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
  packagingOptions {
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile "com.android.support:appcompat-v7:${support_lib_version}"
  compile 'com.eaglesakura:onactivityresult-invoke:1.0.18'
  compile 'com.jakewharton.timber:timber:4.1.1'

  // Rx
  compile 'io.reactivex:rxandroid:1.1.0'
  compile 'io.reactivex:rxjava:1.1.0'
  compile 'io.reactivex:rxkotlin:0.40.1'

  // Kotlin
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
}
buildscript {
  ext.kotlin_version = '1.0.0'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}
repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  maven { url "http://eaglesakura.github.io/maven/" }
}
